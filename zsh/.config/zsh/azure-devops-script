#!/bin/zsh

# Create a pull request to Azure DevOps
function pr() {
    # Prompt for Login if needed
    if ! az account show &> /dev/null; then
        az login --output none
    fi

    # Assign title if provided as argument
    local TITLE="$1"
    # Calculate the last commit message to use in the PR title
    local LAST_COMMIT_MESSAGE=$(git log --pretty=format:"%s" -n 1)
    # Extract the work item id from branch name
    local WORK_ITEM_ID=$(git branch --show-current | tr -cd '0-9')
    # Destructure remote url
    local REMOTE_URL=$(git config --get remote.origin.url)
    local REMOTE_URL_PARTS=($(echo $REMOTE_URL | tr '/' '\n'))
    local PROJECT_NAME=${REMOTE_URL_PARTS[3]}
    local REPO_NAME=${REMOTE_URL_PARTS[4]}

    # Prompt for TITLE if not provided
    if [[ -z "$TITLE" ]]; then
        read "userTitle?Enter title or skip to default to '$LAST_COMMIT_MESSAGE': "
        if [[ -n "$userTitle" ]]; then
            TITLE="$userTitle"
        else
            TITLE="$LAST_COMMIT_MESSAGE"
        fi
    fi

    # Create PR
    echo "ðŸš€ Creating a pull request..."

    # --work-items does not work for some reason
    local RESULT=$(az repos pr create \
        --title "$TITLE" \
        --description "Related work item: #$WORK_ITEM_ID" \
        --draft "true" \
        --work-items "$WORK_ITEM_ID" \
        --output "json" | jq -r .)

    local PULL_REQUEST_ID=$(echo "$RESULT" | jq -r '.pullRequestId')

    if [[ -z "$PULL_REQUEST_ID" ]]; then
        echo "ðŸ’€ Failed to create a pull request"
        return 1
    else
        az repos pr work-item add --id $PULL_REQUEST_ID --work-item-id $WORK_ITEM_ID
        echo "âœ… Successfully created PR:"
        echo "https://dev.azure.com/nzte/$PROJECT_NAME/_git/$REPO_NAME/pullrequest/$PULL_REQUEST_ID"
    fi
}
